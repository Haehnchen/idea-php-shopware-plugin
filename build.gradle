import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "org.jetbrains.intellij" version "0.4.11"
    id 'com.palantir.git-version' version "0.11.0"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

intellij {
    version ideaVersion
    pluginName 'Shopware Plugin'
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            'CSS',
            'java-i18n',
            'properties',
            'yaml',
            'JavaScriptLanguage',
            "java",
            "fr.adrienbrault.idea.symfony2plugin:${symfonyPluginVersion}",
            "de.espend.idea.php.toolbox:${toolboxPluginVersion}",
            "de.espend.idea.php.annotation:${annotationPluginVersion}",
            "com.jetbrains.twig:${twigPluginVersion}"
    ]

    // Can't instantiate configurable for PHP Toolbox
    // at de.espend.idea.php.toolbox.ui.application.ToolboxApplicationForm.<init>(ToolboxApplicationForm.java:26)
    tasks {
        "buildSearchableOptions" {
            enabled = false
        }
    }
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

patchPluginXml {
    sinceBuild '203'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

group 'de.espend.idea.shopware'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '5.5.1'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
